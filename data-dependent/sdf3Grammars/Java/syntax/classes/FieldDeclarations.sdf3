module classes/FieldDeclarations

// 8.3. Field Declarations

imports
  lexical/Identifiers
  types/ReferenceTypes
  types/PrimitiveTypes
  types/ParameterizedTypes
  interfaces/Annotations
  arrays/ArrayInitializers
  expressions/Main

context-free syntax
  
  FieldDeclaration.FieldDecl = <<{FieldModifier " "}*> <UnannType> <{VarDecl ", "}+>;>
  VarDeclId.VariableDecl      = Id
  VarDeclId.VariableDeclArray = <<Id><AnnotatedDim+>>
  VarDecl.VariableDeclInit    = <<VarDeclId> = <VariableInitializer>>
  VarDecl = VarDeclId
  
  VariableInitializer = Expression
  VariableInitializer = ArrayInitializer
  
  FieldModifier           = Annotation
  FieldModifier.Public    = "public"
  FieldModifier.Protected = "protected"
  FieldModifier.Private   = "private"
  FieldModifier.Static    = "static"
  FieldModifier.Final     = "final"
  FieldModifier.Transient = "transient"
  FieldModifier.Volatile  = "volatile"
  
context-free syntax  
  
  UnannType = UnannPrimitiveType
  UnannType = UnannReferenceType
  
  UnannPrimitiveType.NumericType = NumericType
  UnannPrimitiveType.BooleanType = "boolean"
  
  UnannReferenceType = UnannClassType
  UnannReferenceType = UnannArrayType
  
  UnannClassType.ClassType                  = <<Id><TypeArguments?>>
  UnannClassType.ClassOrInterfaceTypeMember = <<UnannClassType>.<{Annotation " "}*> <Id><TypeArguments?>>
  
//  ambiguous with UnannClassType.ClassType
//  UnannTypeVariable.TypeVar            = Id
  
  UnannArrayType.ArrayTypePrimitive    = <<UnannPrimitiveType> <AnnotatedDim+>>
  UnannArrayType.ArrayTypeClassType    = <<UnannClassType> <AnnotatedDim+>>
  
  