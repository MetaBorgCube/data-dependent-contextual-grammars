module classes/ConstructorDeclarations

// 8.8. Constructor Declarations

imports
  classes/ClassDeclarations
  lexical/Identifiers
  classes/MethodDeclarations
  types/ParameterizedTypes
  names/Names
  interfaces/Annotations
  expressions/Main
  statements/Blocks

context-free syntax
  
  ConstructorDeclaration.ConstrDecl = <
  <{ConstructorModifier " "}*> <TypeParameters?> <Id>(<FormalParams>) <Throws?>{
    <ConstructorInvocation?>
    <{BlockStatement "\n"}*>
  }>
  
  ConstructorModifier           = Annotation
  ConstructorModifier.Public    = "public"
  ConstructorModifier.Private   = "private"
  ConstructorModifier.Protected = "protected"
  
  ConstructorInvocation.AltConstrInv      = <<TypeArguments?>this(<{Expression ", "}*>);>
  ConstructorInvocation.SuperConstrInv    = <<TypeArguments?>super(<{Expression ", "}*>);>
  ConstructorInvocation.ExprNameConstrInv = <<Expression>.<TypeArguments?>super(<{Expression ", "}*>);>